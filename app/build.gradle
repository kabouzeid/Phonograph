buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

def gitBranch() {
    def branch = ''
    def proc = 'git rev-parse --abbrev-ref HEAD'.execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    dataBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26

        renderscriptTargetApi 26
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.kabouzeid.gramophone'
        versionCode 136
        versionName '0.16.0 BETA 4'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG (' + gitBranch() + ')'

            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        pro {
            apply plugin: 'io.fabric'
        }
        fdroid {
            applicationIdSuffix ".fdroid"
            versionNameSuffix ' (F-Droid)'

            ext.enableCrashlytics = false
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
}

ext {
    supportLibVersion = '26.0.2'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-compat:$supportLibVersion"
    compile "com.android.support:support-core-utils:$supportLibVersion"
    compile "com.android.support:support-core-ui:$supportLibVersion"
    compile "com.android.support:support-media-compat:$supportLibVersion"
    compile "com.android.support:support-fragment:$supportLibVersion"
    compile "com.android.support:support-v13:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:gridlayout-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"
    compile "com.android.support:preference-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"

//    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.6'
    // TODO: go back to master branch as soon as possible

    compile 'com.android.support.constraint:constraint-layout:+'
    compile 'com.github.kabouzeid:app-theme-helper:1.3.7'
    compile 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    compile 'com.github.kabouzeid:SeekArc:1.2-kmod'
    compile 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'
    compile 'com.afollestad:material-cab:0.1.12'
    compile 'com.github.h6ah4i:android-advancedrecyclerview:feature~support_libraries_v26-SNAPSHOT'
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    compile 'com.heinrichreimersoftware:material-intro:1.6.2'
    compile 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    compile 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'
    compile 'com.jakewharton:butterknife:8.6.0'

    // Only in pro build flavor
    proCompile 'com.anjlab.android.iab.v3:library:1.0.44'
    proCompile 'com.crashlytics.sdk.android:crashlytics:2.6.7'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
}
